/* ***************************************** *
 * Archivo para el Bison 		     *
 * Autores:                          	     *
 *		Eleazar Maestre - 24300665   * 
 *		Luis Gonzalez   - 21241236   *
 * ***************************************** */

/* Librerias usadas por C */
%{ 
	int yylex(); /* Esta declaracion para eliminar los warnings */
	#include <stdio.h>
	#include <stdlib.h>
 	void yyerror(char *);
%}

/* Declaracion de tokens y simbolo inicial */
%token MIL QUINIENTOS CIEN CINCUENTA CINCO DIEZ UNO BLANCO END COMPLE
%start roman

%%

/* Inicio de la gramatica con el simbolo "roman" */
roman:
	/* Descompone el numero desde la denominacion mas grande hasta la mas pequeÃ±a
	   seguido del salto de linea */ 
	thous hunds tens units END{ printf("\e[92m\e[1m%d \e[0m\n",$1+$2+$3+$4);
return 0;}
;

/* Miles - Pueden ser 0, 1, 2 o 3 M */
thous: 
 MIL 			{ $$ = 1000;}
 | MIL MIL 		{ $$ = 2000;}
 | MIL MIL MIL 	{ $$ = 3000;}
 |/*empty*/ 	{ $$=0;}
;

/* Cientos - numeros que van de 0 - 900
   NOTA: de 0 - 300 son generados por la produccion le300
         para simplificar */
hunds:
  CIEN QUINIENTOS		{ $$ = 400;}
| CIEN MIL    			{ $$ = 900;}
| QUINIENTOS  le300 	{ $$ = 500 + $2;} 
| le300      			{ $$ = $1;}
;


le300: 
/*empty*/    		{ $$ = 0;}
|CIEN CIEN CIEN    	{ $$ = 300;}
|CIEN CIEN       	{ $$ = 200;}
|CIEN          		{ $$ = 100;}
;

/* Decenas - numeros que van de 0 - 90
   NOTA: de 0 - 30 son generados por la produccion le30
         para simplificar */
tens: 
le30         		{ $$ = $1;}
|DIEZ CINCUENTA     { $$ = 40;}
|CINCUENTA le30    	{ $$ = 50 + $2;}
|DIEZ CIEN       	{ $$ = 90;}
;



le30:
/*empty*/    		{ $$ = 0;}
|DIEZ DIEZ DIEZ    	{ $$ = 30;} 
|DIEZ DIEZ       	{ $$ = 20;}
|DIEZ          		{ $$ = 10;}
;

/* Unidades - numeros que van de 0 - 9
   NOTA: de 0 - 3 son generados por la produccion le3
         para simplificar */
units:
le3         	{ $$ = $1;}
| UNO CINCO    	{ $$ = 4;}
| CINCO le3   	{ $$ = 5 + $2;}
| UNO DIEZ     	{ $$ = 9;}
;

le3: 
/*empty*/  		{ $$ = 0;}
|UNO UNO UNO  	{ $$ = 3;}
|UNO UNO     	{ $$ = 2;}
|UNO        	{ $$ = 1;}
;

%%

/* NOTA: Los simbolos en los printf() de esta forma \e[1m\e[91m son colores 
		 para la presentacion en terminal */

int main(int argc, char* argv[])
{	
	printf("-> ");
    yyparse();
	printf("\e[0m"); /* Color del terminal original */
	return 0;
}

void yyerror(char *s) 
{
	printf("\e[1m\e[91mERROR: error de sintaxis\n");
}
